apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/imagepicker-release.aar")
            groupId 'com.lzy.imagepicker'
            artifactId 'imagepicker'
            version '1.0'

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')

    publications = ['Production']
    configurations = ['archives']
    override = true

    pkg {
        repo = 'maven'
        name = 'imagepicker'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/toandrew/imagepicker.git'
        publish = true
        dryRun = true
        version {
            name = '1.0'
            desc = 'Init version'
            released  = new Date()
            vcsTag = '1.0.0'
        }
    }
}

//apply from: '../bintray.gradle'